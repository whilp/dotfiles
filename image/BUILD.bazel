load(":debs.bzl", "image_package_files")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load(
    "@io_bazel_rules_docker//container:container.bzl",
    "container_image"
)
load("@python//:requirements.bzl", "requirement")

# NOTE: :image must be built/run separately. We cannot use it
# as data here because this target is expected to be built/run
# in OSX. clipper should be built for OSX and various parts of the
# image (namely nvim) are a PITA to build/extract on OSX.
sh_binary(
    name = "run",
    srcs = ["run.sh"],
    args = [
        "$(location @com_github_wincent_clipper//:clipper)",
    ],
    data = [
        "@com_github_wincent_clipper//:clipper",
    ],
)

container_image(
    name = "image",
    base = "@ubuntu//image",
    debs = image_package_files() + ["@docker//file"],
    # Force rebuild when we observe new packages.
    files = ["//image:debs.bzl"],
    entrypoint = "/usr/bin/nvim",
    workdir = "/home/me",
    env = {
    	"HOME": "/home/me",
    },
    tars = [
        "//home:tar", 
        ":nvim",
        ":etc_ssl_certs",
        ":etc_apt",
        ":usr_bin",
    ],
)

pkg_tar(
    name = "etc_ssl_certs",
    package_dir = "/etc/ssl/certs",
    files = {
        "@cacert//file": "ca-certificates.crt",
    },
)

# https://bugs.launchpad.net/ubuntu/+source/apt/+bug/1577926
pkg_tar(
    name = "etc_apt",
    package_dir = "/etc/apt",
    mode = "0644",
    srcs = [
    	"apt.conf",
    ],
)

pkg_tar(
    name = "usr_bin",
    package_dir = "/usr/bin",
    mode = "0755",
    srcs = [
        "@ripgrep//:rg",
        "@fzf//:fzf",
    ],
    files = {
        "@bazel//file": "bazel",
        "@org_golang_x_tools//cmd/goimports": "goimports",
        "@com_github_jstemmer_gotags//:gotags": "gotags",
        "@com_github_nsf_gocode//:gocode": "gocode",
        "@com_github_josharian_impl//:impl": "impl",
        "@com_github_derekparker_delve//cmd/dlv": "dlv",
        "@com_github_kisielk_errcheck//:errcheck": "errcheck",
        "@com_github_fatih_motion//:motion": "motion",
        "@com_github_rogpeppe_godef//:godef": "godef",
        "@com_github_davidrjenni_reftools//cmd/fillstruct": "fillstruct",
        "@org_golang_x_lint//golint": "golint",
        "@com_github_zmb3_gogetdoc//:gogetdoc": "gogetdoc",
        #"@com_github_bazelbuild_buildtools//buildifier": "buildifier",
        #"@com_github_bazelbuild_buildtools//buildozer": "buildozer",
        #"@com_github_bazelbuild_bazel_watcher//ibazel": "ibazel",
    },
    symlinks = {
        "./usr/bin/python": "python2.7",
        "./bin/nc": "nc.openbsd",
    }
)

genrule(
    name = "nvim",
    outs = ["nvim.tar"],
    cmd = ("$(location @nvim//file) --appimage-extract >/dev/null 2>&1 "
	+ "&& mkdir clean "
	+ "&& mv squashfs-root/usr clean/usr "
        + "&& tar -C clean --sort=name --mtime='2018-01-01 00:00Z' --owner=0 --group=0 --numeric-owner -cf $@ ."
    ),
    tools = [
        "@nvim//file",
    ],
)
